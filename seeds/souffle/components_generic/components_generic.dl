
.comp DiGraph<N> {

    .decl edge(a:N, b:N)
    .decl node(a:N)

    node(X) :- edge(X,_).
    node(X) :- edge(_,X).

    .decl reach(a:N,b:N)
    reach(X,Y) :- edge(X,Y).
    reach(X,Z) :- reach(X,Y), reach(Y,Z).

}

.comp Graph<N> : DiGraph<N> {
    edge(X,Y) :- edge(Y,X).
}


.comp Net<N> {

    .init G = Graph<N>

    .decl e(a:N,b:N)
    G.edge(X,Y) :- e(X,Y).

    .decl r(a:N,b:N)
    r(X,Y) :- G.reach(X,Y).
}


.type city <: symbol
.init StreetMap = Net<city>

StreetMap.e("A","B").
StreetMap.e("B","C").



.type Person = [ name:symbol, address:symbol ]

.init SocialNet = Net<Person>

#define Homer ["Homer","Evergreen Terrace 742"]
#define Ned ["Ned","Evergreen Terrace 744"]
#define Edna ["Ned","Evergreen Terrace 82"]

SocialNet.e(Homer,Ned).
SocialNet.e(Ned,Edna).


.decl result(n:symbol)

result("Map Works") :- StreetMap.r("C","A").
result("Social Net Works") :- SocialNet.r(Edna,Homer).



